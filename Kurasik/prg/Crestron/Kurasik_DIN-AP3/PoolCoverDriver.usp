//Compiler Directives
#CATEGORY "41" // Remote System Interface
#SYMBOL_NAME "PoolCoverDriver"
#HINT ""
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE

// Constants
#DEFINE_CONSTANT PULSE_TIME 5

// Inputs
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT OpenRelay_On_FB;
DIGITAL_INPUT _SKIP_, _SKIP_;
DIGITAL_INPUT CloseRelay_On_FB;
DIGITAL_INPUT _SKIP_, _SKIP_;
DIGITAL_INPUT SafetyBolt_FB;
DIGITAL_INPUT OpenLimit_FB;
DIGITAL_INPUT CloseLimit_FB;
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT EmergencyStop;
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT OpenHold;
DIGITAL_INPUT CloseHold;
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT OpenFull;
DIGITAL_INPUT CloseFull;
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT OpenSwitch;
DIGITAL_INPUT CloseSwitch;

// Outputs
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT OpenRelay_On;
DIGITAL_OUTPUT OpenRelay_Off;
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT CloseRelay_On;
DIGITAL_OUTPUT CloseRelay_Off;
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT PowerRelay_On;
DIGITAL_OUTPUT PowerRelay_Off;
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT moving_FB; // 0 = NO, 1 = YES
DIGITAL_OUTPUT closing_FB;
DIGITAL_OUTPUT opening_FB;
DIGITAL_OUTPUT _SKIP_;
ANALOG_OUTPUT status#_FB; // 0 = closed, 1 = middle, 2 = opened, 3 = locked


// Parameters
INTEGER_PARAMETER pFullTime;

// Parameter Properietes
#BEGIN_PARAMETER_PROPERTIES pFullTime
   propValidUnits = unitTime;
   propDefaultUnit = unitTime;
   propBounds = 10s , 100s;
   propShortDescription = "Full Open/Close Time";
#END_PARAMETER_PROPERTIES


// Structure Definitions

// Global Variables
//INTEGER status#_FB; 
//INTEGER ; 


// Functions

FUNCTION Stop()
{
	PULSE(PULSE_TIME, CloseRelay_Off);
	PULSE(PULSE_TIME, OpenRelay_Off);
}

FUNCTION StartOpen()
{
	Stop();
	delay(5);
	PULSE(PULSE_TIME, OpenRelay_On);	
}

FUNCTION StartClose()
{
	Stop();
	delay(5);
	PULSE(PULSE_TIME, CloseRelay_On);
}

FUNCTION FullOpen()
{
	if (moving_FB = 0 && status#_FB < 2)
	{
		StartOpen();
		WAIT(pFullTime, OPEN_WAIT)
		{
			Stop();
		}
	}
}

FUNCTION FullClose()
{
	if (moving_FB = 0 && (status#_FB = 1 || status#_FB = 2))
	{
		StartClose();
		WAIT(pFullTime, CLOSE_WAIT)
		{
			Stop();
		}
	}
}

FUNCTION StopAndCancel()
{
	CancelAllWait();
    Stop();
}

// 0 = closed, 1 = middle, 2 = opened, 3 = locked
FUNCTION CalculateState()
{
	if (SafetyBolt_FB = 1)
	{
		status#_FB = 3;
		PULSE(PULSE_TIME, PowerRelay_On);
		return;
	}

	if (OpenLimit_FB = 1)
	{
		status#_FB = 2;
		PULSE(PULSE_TIME, PowerRelay_Off);
		return;
	}

	if (CloseLimit_FB = 1)
	{
		status#_FB = 0;
		PULSE(PULSE_TIME, PowerRelay_On);
		return;
	}

	status#_FB = 1;
	PULSE(PULSE_TIME, PowerRelay_Off);
}

FUNCTION CalculateMoving()
{
	if (OpenRelay_On_FB = 1) opening_FB = 1;
	else opening_FB = 0;

	if (CloseRelay_On_FB = 1) closing_FB = 1;
	else closing_FB = 0;

	if (OpenRelay_On_FB = 1 || CloseRelay_On_FB = 1) moving_FB = 1;
	else moving_FB = 0;
}


// Event Handlers

PUSH OpenRelay_On_FB
{
	PULSE(PULSE_TIME, CloseRelay_Off);

	if (status#_FB >= 2) PULSE(PULSE_TIME, OpenRelay_Off);
}

PUSH CloseRelay_On_FB
{
	PULSE(PULSE_TIME, OpenRelay_Off);

	if (status#_FB = 0 || status#_FB = 3) PULSE(PULSE_TIME, CloseRelay_Off);
}

CHANGE OpenRelay_On_FB, CloseRelay_On_FB
{
	CalculateMoving();
}

PUSH SafetyBolt_FB, OpenLimit_FB, CloseLimit_FB
{
	StopAndCancel();
	CalculateState();
}

RELEASE SafetyBolt_FB, OpenLimit_FB, CloseLimit_FB
{
	CalculateState();
}

PUSH EmergencyStop
{
	StopAndCancel();
}

PUSH OpenHold, OpenFull
{
	FullOpen();
}

PUSH CloseHold, CloseFull
{
	FullClose();
}

RELEASE OpenHold, CloseHold
{
	StopAndCancel();
}
  
PUSH OpenSwitch
{
	if (moving_FB = 0) FullOpen();
	else StopAndCancel();
}

PUSH CloseSwitch
{
	if (moving_FB = 0) FullClose();
	else StopAndCancel();
}

// Main Function

Function Main()
{
    status#_FB = 0;
	WaitForInitializationComplete();
	StopAndCancel();;
	CalculateState();
	CalculateMoving();
}
